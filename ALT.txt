//*******************************************************
// Image labelling tool for Detectnet dataset v1.0.0
// Alper ALTINOK, 2017
// ******************************************************
// If you want to label objects with other then "car",
// then simply find "car" word in this code somewhere down below
// and change it to any custom dataset label you want.
// This version of macro is not designed to preserve multiple labels
// like (car, vehicle, pedestrian, etc). Useful only for 
// single label usage.
//*******************************************************
// Action Bar description file :A.L.T

<codeLibrary>
function read_annotation_file(filepath){
        open(filepath);
        selectWindow(File.name);
        getLocationAndSize(x, y, width, height);
        setLocation(screenWidth/2 - width/2, 10, width*(screenHeight-40)/height, screenHeight-40); 
    
        temp = File.nameWithoutExtension;
        fn = File.name;
        path = File.directory;
        textPath = path+temp+".txt";
        call("ij.Prefs.set", "my.path", path);
        call("ij.Prefs.set", "my.filename", fn);
        roiManager("show all with labels");

        if (File.exists(textPath)) {            
            lineseparator = "\n";
            cellseparator = "\t";
			//cellseparator = ",";
            lines=split(File.openAsString(textPath), lineseparator);                        

            // dispatches the data into the new RT
            setBatchMode(true);
            setOption("ExpandableArrays", true);
            for (i=0; i<lines.length; i++) {
                items=split(lines[i], cellseparator);
                label = items[0];
                x = newArray;
                y = newArray;
                index = 0;
                for (j = 1; j < items.length - 1 ; j = j + 2){
                    x[index] = items[j];
                    y[index] = items[j+1];
                    index = index + 1;
                }
                makeSelection("polygon", x, y);
                Roi.setStrokeWidth(1);
                Roi.setStrokeColor('green');
                Roi.setName(items[0]);
                roiManager("Add");
                roiManager("Select", roiManager("Count") -1);
                roiManager("Rename", label);
            }
            roiManager("show all");
			roiManager("UseNames", "true");
            if (lines.length > 1) 
                roiManager("select", 0);

        }
        
        set_and_zoom();
    }

function indexOfArray(array, value) {
    for (a=0; a<lengthOf(array); a++) {
        if (array[a]==value) {
            return a;
        }
    }
    return -1;
}

function set_and_zoom(){
    setTool("Rectangle");        
}    

function close_all(){
    if (isOpen("Log")) { 
        selectWindow("Log"); 
        run("Close"); 
    }

    list = getList("window.titles"); 
    for (i=0; i<list.length; i++){ 
        winame = list[i]; 
        selectWindow(winame); 
        if (winame!="new action bar"){
            run("Close"); 
        }	 
    }
}


function clear_all(){
    close("*");
    if (isOpen("ROI Manager")) {
        selectWindow("ROI Manager");
        run("Close");
    } 
}

function get_image_list(myPath){                
    list = getFileList(myPath);
    imglist = newArray();
    for (i=0; i<list.length; ++i) {
        if (endsWith(list[i], ".jpg") || endsWith(list[i], ".png") || endsWith(list[i], ".jpeg")) {
            imglist=Array.concat(imglist,list[i]);
        }
    }
    return imglist;
}

function alt_open(){
    clear_all();
    filepath=File.openDialog("CHOOSE THE IMAGE TO EDIT / CREATE LABELS");
    read_annotation_file(filepath);
    set_and_zoom();
}

function alt_save(){
    mainTitle = getTitle();
    setBatchMode(true);
    output_dir = getDirectory("image");
    title1 = "annotation_table";
    title2 = "[" + title1 + "]";
    run("New... ", "name=" + title2 + " type=Table"); 

    nCount = roiManager('count')            
    for (i=0; i<nCount; ++i) { 
        roiManager("select", i)
        Roi.getCoordinates(x,y);
        labeltype = Roi.getName;
        location = "";
        for (j=0; j< x.length; j++){
            if (j != 0){
                location = location + '\t';
             }
             location = location + x[j] + '\t' + y[j];
        }
        print(title2, labeltype + "\t" + location);
    }
    selectWindow(title1);
    newtitle = replace(mainTitle, ".jpg", ".txt");
    saveAs("text", output_dir + File.separator + newtitle);
    selectWindow(title1);
    run("Close");
}

function alt_prev(){
    myPath = call("ij.Prefs.get", "my.path", 0);
    myFilename = call("ij.Prefs.get", "my.filename", 1);
    imglist = get_image_list(myPath);
    fileorder = indexOfArray(imglist, myFilename);
    if (fileorder > 0) {
        fileorder = fileorder - 1;
    }else {
        fileorder = imglist.length - 1;
    }
    myFilename=imglist[fileorder]; 

    clear_all();
    read_annotation_file(myPath+File.separator+myFilename);
}

function alt_next(){
    myPath = call("ij.Prefs.get", "my.path", 0);
    myFilename = call("ij.Prefs.get", "my.filename", 1);
    imglist = get_image_list(myPath);
    fileorder=indexOfArray(imglist, myFilename);        
    if (fileorder < imglist.length-1) {
        fileorder = fileorder + 1;
    }else {
        fileorder = 0;
    }
    myFilename=imglist[fileorder];

    clear_all(); 
    read_annotation_file(myPath+File.separator+myFilename);
}

function alt_add(){
    if (getInfo("selection.name") != "") {        
           waitForUser("This region has label");    
    }else{
        size = getValue("selection.size");
        if (size == 0 ){
            waitForUser("There are no selected region");    
        }else{
            getSelectionCoordinates(x, y);    
            Dialog.create("New label");
            Dialog.addString("Text:", "");
            Dialog.show();
            label = Dialog.getString();
            Roi.setName(label);
            roiManager("Add");
            roiManager("Select", roiManager("Count") -1);
            roiManager("Rename", label);
            roiManager("show all");
        }
    }
}

function alt_direct_add(label){
    if (getInfo("selection.name") != "") {
           waitForUser("This region has label");
    }else{
        size = getValue("selection.size");
        if (size == 0 ){
            waitForUser("There are no selected region");
        }else{
            getSelectionCoordinates(x, y);
            Roi.setName(label);
            roiManager("Add");
            roiManager("Select", roiManager("Count") -1);
            roiManager("Rename", label);
            roiManager("show all");
        }
    }
}


function alt_delete(){
    if (getInfo("selection.name") == "") {
        selection_size = getValue("selection.size");
        if (selection_size == 0){
            waitForUser("There are no selected region");    
        }else{
            setBatchMode(true);
            getSelectionBounds(x,y, width, height);        
            bottom_right_x = x + width;
            bottom_right_y = y + height ; 
            nCount = roiManager('count');
            for (i = nCount-1 ; i >= 0 ; i --){
                roiManager('select', i);
                Roi.getBounds(bx, by, bwidth, bheight)	
                if( x <= bx && y<= by && bx + bwidth <= bottom_right_x && by+bheight <= bottom_right_y){
                    roiManager('delete'); 
                    Roi.remove;
                }
            }
        }
    }else{
        roiManager('delete');
    }

}

function alt_edit(){
    if (getInfo("selection.name") == ""){
        waitForUser("There are no selected region");
    }else{
        old_fill_color = Roi.getFillColor();
        Roi.setFillColor("5500ff00")
        Dialog.create("edit label");
        Dialog.addString("Text:", Roi.getName);
        Dialog.show();
        label = Dialog.getString();
        Roi.setName(label);
        roiManager("rename", label);
        Roi.setFillColor(old_fill_color)
        roiManager("show all");
    }
}

function alt_find(){
    if (getInfo("selection.name") == ""){
        waitForUser("There are no selected region");
    }else{
        old_fill_color = Roi.getFillColor();    
        for ( i = 0 ; i < 5 ; i ++ ){            
            Roi.setFillColor("5500ff00");         
            wait(100);
            Roi.setFillColor("55ff4400");
            wait(100);
        }
        Roi.setFillColor(old_fill_color);
    }
}
</codeLibrary>

<line>
<button> 1 line 1
label=Choose an image file to start/continue labeling
icon=alt/open.png
arg=<macro>
    alt_open();
</macro>


<button> 2 line 1
label= save edited labels
icon=alt/save.png
arg=<macro>
    alt_save();
</macro>

<button> 3 line 1
label=Prev image (jpeg or png)
icon=alt/prev.png
arg=<macro>  
     alt_prev();
</macro>


<button> 4 line 1
label=Next image (jpeg or png)
icon=alt/next.png
arg=<macro>  
    alt_next();
</macro>

<button> 5 line 1
label= add label
icon=alt/add.png
arg=<macro>
    alt_add();
</macro>

<button> 6 line 1
label= delete rectangle
icon=alt/delete.png
arg=<macro> 
    alt_delete();
</macro>

<button> 7 line 1
label= edit label
icon=alt/edit.png
arg=<macro> 
    alt_edit();
</macro>

<button> 8 line 1
label= find region
icon=alt/find.png
arg=<macro> 
    alt_find();
</macro>
</line>
// end of file